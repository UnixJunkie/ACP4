
set tics out nomirror
set xtics 0,2

set xrange [1:24]

set title 'Dual-core motherboard; 12 threads per CPU'

set xlabel 'Parallel worker domains (+2 for total domains)'
set ylabel 'Processing frequency (Hz)'

# set term postscript eps enhanced color
set term pngcairo
set output 'pix/xp_par_encode.png'

set key left
set style fill transparent solid 0.5 noborder
plot 'data/xp_par_encode.txt' u 1:($5-$6):($5+$6) w filledcurves not lc 1, \
     ''                       u 1:5 w l t 'ocaml-4.13.1'             lc 1, \
     ''                       u 1:($3-$4):($3+$4) w filledcurves not lc 2, \
     ''                       u 1:3 w l t 'ocaml-5.0.0'              lc 2
